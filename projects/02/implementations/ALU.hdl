// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Pipe X value
    Mux16(a=x, b=false, sel=zx, out=zxVal);        
    Not16(in=zxVal, out=negNxVal);
    Mux16(a=zxVal, b=negNxVal, sel=nx, out=xVal);  
    
    // Pipe Y value
    Mux16(a=y, b=false, sel=zy, out=zyVal);        
    Not16(in=zyVal, out=negYVal);
    Mux16(a=zyVal, b=negYVal, sel=ny, out=yVal);   
    
    // Possible function inputs computed
    Add16(a=xVal, b=yVal, out=addXy);
    And16(a=xVal, b=yVal, out=andXy);

    // Possible function outputs computed
    Mux16(a=andXy, b=addXy, sel=f, out=functionOut);
    Not16(in=functionOut, out=negFunctionOut);

    // ALU function output result
    Mux16(a=functionOut, b=negFunctionOut, sel=no, out=fout); // rename var for later use
    Mux16(a=fout, b=false, sel=false, out=out);

    // Return value is all zeroes
    Or16Way(in=fout, out=isNonZero); // Custom chip -> takes 16, returns true if any exist
    Xor(a=isNonZero, b=true, out=zr);

    // Return value is negative
    Neg16(in=fout, out=isNegative); // Custom chip -> takes 16, returns negative if final bit exists
    Xor(a=isNegative, b=false, out=ng);
}
